#version 430
//! layout(local_size_x = 16, local_size_y = 16) in; // this is inserted on load
layout(rgba32f, binding = 0) writeonly uniform image2D img_output;
layout(r8ui, binding = 1) readonly uniform uimage3D blockData;



//! #define WORLD_SIZE 64
//! #define WORLD_HEIGHT 64
//! #define TEXTURE_RES 16
//! #define RENDER_DIST 100.0

#define BLOCK_AIR 0
#define BLOCK_MIRROR 9
uniform sampler2D textureAtlas;

struct Camera
{
    vec3 pos;
    float cosYaw;
    float cosPitch;
    float sinYaw;
    float sinPitch;
    vec2 frustumDiv;
};
uniform Camera camera;

uniform vec2 screenSize;


// lighting
uniform vec3 lightDirection;
uniform vec3 ambColor;
uniform vec3 skyColor;
uniform vec3 sunColor;

// get the block at the specified position in the world
int getBlock(ivec3 coords)
{
    return int(imageLoad(blockData, coords).x);
}

bool inWorld(ivec3 pos)
{
    const vec3 lessThanWorld = step(vec3(0, -2, 0), pos);
    const vec3 greaterThanWorld = step(vec3(WORLD_SIZE, WORLD_HEIGHT, WORLD_SIZE), pos);

    return dot(lessThanWorld, lessThanWorld) * dot(greaterThanWorld, greaterThanWorld) == 0;
}

#define AXIS_X 0
#define AXIS_Y 1
#define AXIS_Z 2

#define updateVelocityFields()\
    ijk = ivec3(start);\
    ijkStep = ivec3(sign(velocity));\
    vInverted = abs(1 / velocity);\
    dist = -fract(start) * ijkStep;\
    dist += max(ijkStep, vec3(0));\
    dist *= vInverted


// ~~stolen~~ took "inspiration" from https://github.com/Vercidium/voxel-ray-marching/blob/master/source/Map.cs

// Voxel ray marching from http://www.cse.chalmers.se/edu/year/2010/course/TDA361/grid.pdf
// Optimized by keeping block lookups within the current chunk, which minimizes bitshifts, masks and multiplication operations
vec3 rayMarch(in vec3 start, in vec3 velocity, in float maximum, in vec3 fogColor, out bool hit, out vec3 hitPos, out float rayTravelDist)
{
    // Determine the chunk-relative position of the ray using a bit-mask
    ivec3 ijk;

    // The amount to increase i, j and k in each axis (either 1 or -1)
    ivec3 ijkStep;

    // This variable is used to track the current progress throughout the ray march
    vec3 vInverted;

    // The distance to the closest voxel boundary in units of rayTravelDist
    vec3 dist;

    updateVelocityFields();

    int axis = AXIS_X;

    rayTravelDist = 0;

    while (rayTravelDist <= maximum)
    {
        // Exit check
        if(!inWorld(ijk))
            break;

        int blockHit = getBlock(ijk);

        if (blockHit != BLOCK_AIR)
        {
            hitPos = start + velocity * rayTravelDist;
            
            if(blockHit == BLOCK_MIRROR)
            {
                velocity[axis] = -velocity[axis]; // reflect!
                start = hitPos;

                updateVelocityFields();
            } else {

                // side of block
                int texFetchX = int(mod((hitPos.x + hitPos.z) * TEXTURE_RES, TEXTURE_RES));
                int texFetchY = int(mod(hitPos.y * TEXTURE_RES, TEXTURE_RES) + TEXTURE_RES);


                if (axis == AXIS_Y) // we hit the top/bottom of block
                {
                    texFetchX = int(mod(hitPos.x * TEXTURE_RES, TEXTURE_RES));
                    texFetchY = int(mod(hitPos.z * TEXTURE_RES, TEXTURE_RES));

                    if (velocity.y < 0.0F) // looking at the underside of a block
                        texFetchY += TEXTURE_RES * 2;
                }

                vec3 textureColor = vec3(texture(textureAtlas,
                                                vec2((texFetchX + (blockHit * TEXTURE_RES) + 0.5) / float(TEXTURE_RES * 16.0),
                                                (texFetchY + 0.5) / float(TEXTURE_RES * 3.0))));
            

                if (dot(textureColor, textureColor) != 0) { // pixel is not transparent
                
                    hit = true;
                    hitPos = start + velocity * (rayTravelDist - 0.01f);


                    float lightIntensity = 1 + (-sign(velocity[axis]) * lightDirection[axis]) / 2.0f;

#ifdef CLASSIC
                    float fogIntensity = ((rayTravelDist / RENDER_DIST)) * (0xFF - (axis + 2) % 3 * 50) / 0xFF;
                    return mix(textureColor, fogColor, fogIntensity);
#else
                    

                    
                    return textureColor;// * mix(ambColor, sunColor, lightIntensity);
#endif
                }
            }
        }

        // Determine the closest voxel boundary
        if (dist.y < dist.x)
        {
            if (dist.y < dist.z)
            {
                // Advance to the closest voxel boundary in the Y direction

                // Increment the chunk-relative position and the block access position
                ijk.y += ijkStep.y;

                // Update our progress in the ray 
                rayTravelDist = dist.y;

                // Set the new distance to the next voxel Y boundary
                dist.y += vInverted.y;

                // For collision purposes we also store the last axis that the ray collided with
                // This allows us to reflect particle velocity on the correct axis
                axis = AXIS_Y;
            }
            else
            {
                ijk.z += ijkStep.z;

                rayTravelDist = dist.z;
                dist.z += vInverted.z;
                axis = AXIS_Z;
            }
        }
        else if (dist.x < dist.z)
        {
            ijk.x += ijkStep.x;

            rayTravelDist = dist.x;
            dist.x += vInverted.x;
            axis = AXIS_X;
        }
        else
        {
            ijk.z += ijkStep.z;

            rayTravelDist = dist.z;
            dist.z += vInverted.z;
            axis = AXIS_Z;
        }
    }

    hit = false;

#ifdef CLASSIC
    return vec3(0);
#else
    return fogColor; // sky
#endif
}

vec3 getPixel(in vec2 pixel_coords)
{
    const vec2 frustumRay = (pixel_coords - (0.5 * screenSize)) / camera.frustumDiv;

    // rotate frustum space to world space
    const float temp = camera.cosPitch + frustumRay.y * camera.sinPitch;
    
    vec3 rayDir = normalize(vec3(frustumRay.x * camera.cosYaw + temp * camera.sinYaw,
                                 frustumRay.y * camera.cosPitch - camera.sinPitch,
                                 temp * camera.cosYaw - frustumRay.x * camera.sinYaw));


    const vec3 fogColor = mix(skyColor, sunColor, 0.5 * pow(clamp(dot(rayDir, lightDirection), 0, 1) + 0.2, 5));

    // raymarch outputs
    vec3 hitPos;
    bool hit;
    float hitDist;
    vec3 color = rayMarch(camera.pos, rayDir, RENDER_DIST, fogColor, hit, hitPos, hitDist);

#ifndef CLASSIC
    if(hit)
    {
        float shadowMult = (1 - lightDirection.y) * 0.3;

        if(lightDirection.y < 0) { // day
            float ignoreHitDist = 0;
            vec3 ignoreColor = rayMarch(hitPos, lightDirection, RENDER_DIST / 2, fogColor, hit, hitPos, ignoreHitDist);


            if(hit) // we can't see the sun
                shadowMult *= 1 - -lightDirection.y * 0.3;
            
        } else {
            shadowMult = (1 - lightDirection.y) * 0.3; // night
        }

        color = color * shadowMult; // apply shadow

        if(hitDist > RENDER_DIST * 0.95) {
            float fogIntensity = ((hitDist - RENDER_DIST * 0.95) / (RENDER_DIST * 0.05));

            color = mix(color, fogColor, fogIntensity);
        }
    }
#endif

    return color;
}

void main() {
    // get index in global work group i.e x,y position
    ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
    
    vec4 pixel = vec4(getPixel(pixel_coords), 1);

    // output to image
    imageStore(img_output, pixel_coords, pixel);
}